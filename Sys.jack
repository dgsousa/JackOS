// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
        do Memory.init();
        do Keyboard.init();
        do Screen.init();
        do Output.init();
        do Math.init();
        do Main.main();
        do Sys.halt();
        return;
    }

    /** Halts the program execution. */
    function void halt() {
        while(true) {}
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int i, j;
        let i = 0;
        while(i < duration) {
            let j = 0;
            while(j < 200) {
                let j = j + 1;
            }
            let i = i + 1;
        }
        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        var String intVal, errorString, err;
        var int counter, length;
        let intVal = String.new(6);
        let errorString = String.new(9);
        let err = "ERR";
        do intVal.setInt(errorCode);
        let length = intVal.length();
        let counter = 0;
        let errorString = errorString.appendChar(err.charAt(0));
        let errorString = errorString.appendChar(err.charAt(1));
        let errorString = errorString.appendChar(err.charAt(2));
        while(counter < length) {
            let errorString = errorString.appendChar(intVal.charAt(counter));
            let counter = counter + 1;
        }
        do Output.printString(errorString);
        do Sys.halt();
        return;
    }
}
